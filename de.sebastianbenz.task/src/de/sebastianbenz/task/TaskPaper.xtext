grammar de.sebastianbenz.task.TaskPaper

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate taskPaper "http://www.sebastianbenz.de/task/TaskPaper"

Todo:
	{Todo}
	(contents += Content)+
	WS*;

Content:
	=> Project | 
	=> Task | 
	Note;

Task:
	(=> ClosedTask) | OpenTask;

OpenTask:
	{OpenTask}
	text=TASK_OPEN;
	
ClosedTask:
	{ClosedTask}
	text=TASK_CLOSED;


//DoneTask:
//	text=TASK_DONE;
//
Note:
	text=NOTE_CONTENT;

Project:
	{Project}
	=> (intend+=WS)* text=(PROJECT_1 | PROJECT_2 | PROJECT_3);

Spaces:
	text=SPACE
;

	
terminal WS:
	' ' | '\t';

terminal SPACE:
	WS* NL
;

terminal NOTE_CONTENT:
	 WS* !(TASK_START|'\n'|'\r' | WS) !('\n'|'\r')* !(PROJECT_DELIMITER | '\n'|'\r') NL 
;

terminal TASK_OPEN:	
	 WS* TASK_START !('@' | '\n'|'\r')* NL;

terminal TASK_CLOSED:	
	 WS* (TASK_START !('\n'|'\r' | '@')+  '@done' !('\n'|'\r'| '@')+  NL) |
	 WS* (TASK_START !('\n'|'\r'| '@')+ '@done' NL) |
	 WS* (TASK_START '@done' !('\n'|'\r'| '@')+  NL) 
	;

terminal PROJECT_1:
	!('\n'|'\r'|TASK_START|WS) !('\n'|'\r')* PROJECT_DELIMITER NL;

terminal PROJECT_2:
	WS !('\n'|'\r'|TASK_START|WS) !('\n'|'\r')* PROJECT_DELIMITER NL;
	
terminal PROJECT_3:
	WS WS+ !('\n'|'\r'|TASK_START|WS) !('\n'|'\r')* PROJECT_DELIMITER NL;

terminal fragment PROJECT_DELIMITER:
	':';

terminal fragment NL:
	'\r'? '\n';

terminal fragment TASK_START:
	'-';
	


