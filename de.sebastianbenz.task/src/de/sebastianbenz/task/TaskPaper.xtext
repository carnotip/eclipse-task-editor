grammar de.sebastianbenz.task.TaskPaper 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate taskPaper "http://www.sebastianbenz.de/task/TaskPaper"

Todo:
	{Todo}
	(contents += Content | Spaces)*
	WS*;
/*
Project
WS TASK
WS PROJECT
WS WS TASK
WS WS PROJECT
 */

Content:
	Project |
	Task | 
	Note 
	;

Task:
	(intend+=WS)* text=TASK_TEXT;

Note:
	(intend+=WS)* text=TEXT;

Project:
	(intend+=WS)* text=PROJECT_
;

Spaces:
	(WS)* NL
;
	
terminal WS				:	' ' | '\t';
terminal NL				:	'\r'? '\n';
	
terminal TASK_TEXT		: HYPHEN !('\n'|'\r')* NL;
terminal TEXT			: !(HYPHEN|'\n'|'\r' | WS) !('\n'|'\r')* !(COLON | '\n'|'\r') NL;
terminal PROJECT_		: !('\n'|'\r'|HYPHEN|WS) !('\n'|'\r')* COLON NL;		

terminal fragment COLON : ':';

terminal fragment HYPHEN: '-';
	


